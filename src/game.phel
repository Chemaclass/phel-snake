(ns phel-snake\game
  (:use RuntimeException)
  (:require phel-cli-gui\terminal-gui)
  (:require phel-snake\logic :refer [generate-new-goal
                                     generate-snake-head
                                     normalize-next-direction
                                     move-snake
                                     update-goal
                                     collision-with-board?
                                     grow-snake]))

(terminal-gui/add-output-formatter {:style-name "snake-head" :foreground "black" :background "red" :options ["bold"]})
(terminal-gui/add-output-formatter {:style-name "snake-tail" :foreground "black" :background "green" :options ["bold"]})
(terminal-gui/add-output-formatter {:style-name "goal" :foreground "black" :background "magenta" :options ["bold"]})

(def difficulty
  "The higher the faster it will increase the speed on each level."
  10000)

(def nano-seconds-delay-base 100000)
(def start-time (php/microtime true))
(def goal-icon {:text " " :style "goal"})
(def snake-head-icon {:text " " :style "snake-head"})
(def snake-tail-icon {:text " " :style "snake-tail"})

(defn debug-snake [board snake]
  (terminal-gui/clear-line (+ 3 (board :height)))
  (terminal-gui/render 0 (+ 3 (board :height)) (str "# snake" snake)))

(defn sleep-delay [snake]
  (let [velocity (* (snake :speed) difficulty)
        nano-sec (- nano-seconds-delay-base velocity)]
    (php/usleep nano-sec)))

(defn render-snake [snake]
  (for [t :in (snake :tail)]
    (terminal-gui/render (t :x) (t :y) (snake-tail-icon :text) (snake-tail-icon :style)))
  (let [head (snake :head)]
    (terminal-gui/render (head :x) (head :y) (snake-head-icon :text) (snake-head-icon :style))))

(defn render-goal [goal]
  (terminal-gui/render (goal :x) (goal :y) (goal-icon :text) (goal-icon :style)))

(defn render-game-over [board snake]
  (let [game-over-text "GAME OVER"
        game-over-text-len (php/strlen game-over-text)
        x-center (php/round (- (/ (board :width) 2) (/ game-over-text-len 2)))
        y-center (php/round (/ (board :height) 2))]
    (render-snake snake)
#    (debug-snake board snake)
    (terminal-gui/render x-center y-center game-over-text)
    (terminal-gui/render x-center (inc y-center) (format "Points: %d" (snake :speed)))
    (throw (php/new RuntimeException "So much fun!"))))

(defn playing-time []
  (let [diff-time (- (php/microtime true) start-time)]
    (format "Time: %d secs" (php/round diff-time))))

(defn render-stats [board snake]
  (terminal-gui/clear-line (+ 1 (board :height)))
  (terminal-gui/render 0 (+ 1 (board :height)) (format "Speed: %d" (count (snake :tail))))
  (terminal-gui/clear-line (+ 2 (board :height)))
  (terminal-gui/render 0 (+ 2 (board :height)) (playing-time)))

(defn main []
  (terminal-gui/clear-screen)
  (loop [board {:width 45 :height 20}
         snake {:direction :right
                :speed 1
                :head (generate-snake-head board)
                :tail []}
         goal (generate-new-goal board)]

    (sleep-delay snake)
    (terminal-gui/clear-output)
    (terminal-gui/render-board board)

    (let [{:hex in} (terminal-gui/read-input 3)
          next-direction (normalize-next-direction in)
          updated-goal (update-goal snake goal board)
          updated-snake (move-snake snake next-direction)]

      (if (collision-with-board? board snake)
        (render-game-over board snake))

      (render-snake updated-snake)
      (render-goal updated-goal)
      (render-stats board updated-snake)
#      (debug-snake board updated-snake)

      (let [new-goal? (not= goal updated-goal)
            sn (if new-goal?
                 (update updated-snake :speed inc)
                 updated-snake)
            sn2 (if new-goal? (grow-snake sn) sn)]
        (recur board sn2 updated-goal)))))

(try
  (main)
  (catch RuntimeException e
    (println (php/-> e (getMessage)))))
