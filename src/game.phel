(ns phel-snake\game
  (:require phel-cli-gui\terminal-gui :refer [clear-screen
                                              read-input
                                              render-board
                                              render])
  (:require phel-snake\logic :refer [generate-new-goal
                                     normalize-next-direction
                                     move-snake
                                     update-goal
                                     collision-with-board?
                                     grow-snake]))

(def start-time (php/microtime true))
(def nano-seconds-delay 200000)
(def goal-icon "A")
(def snake-head-icon "X")
(def snake-tail-icon "x")

(defn sleep-delay [snake]
  (let [velocity (* (snake :points) 15000)
        nano-sec (- nano-seconds-delay velocity)]
    (php/usleep nano-sec)))

(defn render-snake [snake]
  (render (get-in snake [:head :x]) (get-in snake [:head :y]) snake-head-icon)
  (for [t :in (snake :tail)]
    (render (t :x) (t :y) snake-tail-icon)))

(defn render-goal [goal]
  (render (goal :x) (goal :y) goal-icon))

(defn render-game-over [board snake]
  (let [game-over-text "GAME OVER"
        game-over-text-len (php/strlen game-over-text)
        x-center (- (/ (board :width) 2) (/ game-over-text-len 2))
        y-center (/ (board :height) 2)]
    (render x-center y-center game-over-text)
    (render x-center (inc y-center) (format "Points: %d" (snake :points)))
    (php/die "So much fun!\n")))

(defn playing-time []
  (let [diff-time (- (php/microtime true) start-time)]
    (format "Time: %d secs" (php/round diff-time))))

(defn clean-line [y]
  (render 0 y (php/str_repeat " " 100)))

(defn render-stats [board snake]
  (clean-line (+ 1 (board :height)))
  (render 0 (+ 1 (board :height)) (format "Level: %d" (snake :points)))
  (clean-line (+ 2 (board :height)))
  (render 0 (+ 2 (board :height)) (playing-time)))

(defn debug-snake [board snake]
  (clean-line (+ 3 (board :height)))
  (render 0 (+ 3 (board :height)) (str "# snake" snake)))

(defn main []
  (clear-screen)

  (loop [board {:width 40 :height 20}
         snake {:head {:x 2 :y 2}
                :tail []
                :direction :right
                :points 0}
         goal (generate-new-goal board)]

    (sleep-delay snake)
    (render-board board)

    (let [{:hex in} (read-input 3)
          next-direction (normalize-next-direction in)
          updated-snake (move-snake snake next-direction)
          updated-goal (update-goal snake goal board)]

      (if (collision-with-board? board snake)
        (render-game-over board snake))

      (render-snake updated-snake)
      (render-goal updated-goal)
      (render-stats board updated-snake)
      #(debug-snake board updated-snake)

      (let [new-goal? (not= goal updated-goal)
            sn (if new-goal?
                 (update-in updated-snake [:points] inc)
                 updated-snake)
            sn2 (if new-goal? (grow-snake sn) sn)]
        (recur board sn2 updated-goal)))))

(main)
