(ns phel-snake\game
  (:use RuntimeException)
  (:require phel-cli-gui\terminal-gui :as gui)
  (:require phel-snake\logic :as l))

(gui/add-output-formatter {:style-name "snake-head" :foreground "black" :background "red" :options ["bold"]})
(gui/add-output-formatter {:style-name "snake-tail" :foreground "black" :background "green" :options ["bold"]})
(gui/add-output-formatter {:style-name "goal" :foreground "black" :background "magenta" :options ["bold"]})

(def difficulty
  "The higher the faster it will increase the speed on each level."
  10000)

(def board-width (l/get-from-list argv "width" 42))
(def board-height (l/get-from-list argv "height" 22))
(def debug? (php/in_array "debug" argv))
(def nano-seconds-delay-base 140000)
(def start-time (php/microtime true))
(def goal-icon {:text " " :style "goal"})
(def snake-head-icon {:text " " :style "snake-head"})
(def snake-tail-icon {:text " " :style "snake-tail"})

(defn debug-snake [board snake]
  (if debug? (do (gui/clear-line (+ 3 (board :height)))
                 (gui/render 0 (+ 3 (board :height)) (str "# snake" snake)))))

(defn sleep-delay [snake]
  (let [velocity (* (snake :speed) difficulty)
        nano-sec (- nano-seconds-delay-base velocity)
        velocity-normalizer (case (snake :direction)
                              :left 2 :right 2
                              :up 1 :down 1)
        sleep-time (/ nano-sec velocity-normalizer)]
    (if debug? (do (println)
                   (println "# sleep-time:" sleep-time "| velocity-normalizer:" velocity-normalizer)
                   (println)(println)(println "playing...")))
    (php/usleep sleep-time)))

(defn render-snake [snake]
  (for [t :in (snake :tail)]
    (gui/render (t :x) (t :y) (snake-tail-icon :text) (snake-tail-icon :style)))
  (let [head (snake :head)]
    (gui/render (head :x) (head :y) (snake-head-icon :text) (snake-head-icon :style))))

(defn render-goal [goal]
  (gui/render (goal :x) (goal :y) (goal-icon :text) (goal-icon :style)))

(defn render-game-over [board snake]
  (let [game-over-text "GAME OVER"
        game-over-text-len (php/strlen game-over-text)
        x-center (php/round (- (/ (board :width) 2) (/ game-over-text-len 2)))
        y-center (php/round (/ (board :height) 2))]
    (render-snake snake)
    (debug-snake board snake)
    (gui/render x-center y-center game-over-text)
    (gui/render x-center (inc y-center) (format "Points: %d" (snake :speed)))
    (throw (php/new RuntimeException "\n\nSo much fun!"))))

(defn playing-time []
  (let [diff-time (- (php/microtime true) start-time)]
    (format "Time: %d secs" (php/round diff-time))))

(defn render-stats [board snake]
  (println (format "Speed: %d" (count (snake :tail))))
  (println (playing-time)))

(defstruct snake [direction speed head tail])

(defn main []
  (gui/clear-screen)
  (loop [board {:width board-width :height board-height}
         s {:old {}
            :new (snake :right 1 (l/generate-snake-head board) [])}
         goal (l/generate-new-goal board)]

    (sleep-delay (s :new))
    (gui/clear-output)
    (gui/render-board board)

    (let [{:hex in} (gui/read-input 3)
          next-direction (l/normalize-next-direction in)
          updated-goal (l/update-goal (s :new) goal board)
          updated-snake (l/move-snake (s :new) next-direction)]

      (if (l/collision-with-board? board (s :new))
        (render-game-over board (s :new)))

      (render-snake updated-snake)
      (render-goal updated-goal)
      (render-stats board updated-snake)
      (debug-snake board updated-snake)

      (let [new-goal? (not= goal updated-goal)
            sn (if new-goal?
                 (update updated-snake :speed inc)
                 updated-snake)
            sn2 (if new-goal? (l/grow-snake sn) sn)]
        (recur board {:old s :new sn2} updated-goal)))))

(when-not *compile-mode*
  (try
    (main)
    (catch RuntimeException e
      (println)
      (println (php/-> e (getMessage))))))
